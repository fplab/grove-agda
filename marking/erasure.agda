open import marking.prelude

open import marking.id
open import marking.typ
open import marking.gtyp
open import marking.uexp
open import marking.mexp

module marking.erasure where
  mutual
    _⇒□ : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇒ τ ∣ 𝕞) → UExp
    (⊢_^_ {x = x} ∋x u)      ⇒□ = - x ^ u
    (⊢λ x ∶ τ ∙ ě ^ u)       ⇒□ = -λ x ∶ τ ∙ (ě ⇒□s) ^ u
    (⊢ ě₁ ∙ ě₂ [ τ▸ ]^ u)    ⇒□ = - (ě₁ ⇒□s) ∙ (ě₂ ⇐□s) ^ u
    (⊢⸨ ě₁ ⸩∙ ě₂ [ τ!▸ ]^ u) ⇒□ = - (ě₁ ⇒□s) ∙ (ě₂ ⇐□s) ^ u
    (⊢ℕ n ^ u)               ⇒□ = -ℕ n ^ u
    (⊢ ě₁ + ě₂ ^ u)          ⇒□ = - (ě₁ ⇐□s) + (ě₂ ⇐□s) ^ u
    (⊢⟦_⟧^_ {y = y} ∌y u)    ⇒□ = - y ^ u
    (⊢⋎^ u)                  ⇒□ = -⋎^ u
    (⊢↻^ u)                  ⇒□ = -↻^ u

    _⇒□s : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇒s τ ∣ 𝕞) → USubExp
    (⊢□^ w ^ p)    ⇒□s = -□^ w ^ p
    (⊢∶ ⟨ w , ě ⟩) ⇒□s = -∶ (⟨ w , ě ⇒□ ⟩)
    (⊢⋏ ė*)        ⇒□s = -⋏ (ė* ⇒□s*)

    _⇒□s* : ∀ {Γ} → (ė* : List (EdgeId × ∃[ τ ] ∃[ 𝕞 ] Γ ⊢⇒ τ ∣ 𝕞)) → List USubExp'
    []                       ⇒□s* = []
    (⟨ w , ⟨ _ , ⟨ _ , ě ⟩ ⟩ ⟩ ∷ ė*) ⇒□s* = ⟨ w , ě ⇒□ ⟩ ∷ (ė* ⇒□s*)

    _⇐□ : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇐ τ ∣ 𝕞) → UExp
    (⊢λ x ∶ τ ∙ ě [ τ₃▸ ∙ τ~τ₁ ]^ u)   ⇐□ = -λ x ∶ τ ∙ (ě ⇐□s) ^ u
    (⊢⸨λ x ∶ τ ∙ ě ⸩[ τ'!▸ ]^ u)       ⇐□ = -λ x ∶ τ ∙ (ě ⇐□s) ^ u
    (⊢λ x ∶⸨ τ ⸩∙ ě [ τ₃▸ ∙ τ~̸τ₁ ]^ u) ⇐□ = -λ x ∶ τ ∙ (ě ⇐□s) ^ u
    ⊢⸨ ě ⸩[ τ~̸τ' ∙ su ]                ⇐□ = ě ⇒□
    ⊢∙ ě [ τ~τ' ∙ su ]                 ⇐□ = ě ⇒□

    _⇐□s : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇐s τ ∣ 𝕞) → USubExp
    ⊢∙ ě [ τ~τ' ] ⇐□s  = ě ⇒□s
    ⊢⸨ ě ⸩[ τ~̸τ' ] ⇐□s = ě ⇒□s

  mutual
    ⊢⇐-⊢⇒ : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇐ τ ∣ 𝕞) → ∃[ τ' ] ∃[ 𝕞' ] Σ[ ě' ∈ Γ ⊢⇒ τ' ∣ 𝕞' ] ě ⇐□ ≡ ě' ⇒□
    ⊢⇐-⊢⇒ (⊢λ x ∶ τ ∙ ě [ τ₃▸ ∙ τ~τ₁ ]^ u)
      with ⟨ τ' , ⟨ _ , ⟨ ě' , eq ⟩ ⟩ ⟩ ← ⊢⇐s-⊢⇒s ě rewrite eq
         = ⟨ (τ △) -→ τ' , ⟨ _ , ⟨ ⊢λ x ∶ τ ∙ ě' ^ u , refl ⟩ ⟩ ⟩
    ⊢⇐-⊢⇒ (⊢⸨λ x ∶ τ ∙ ě ⸩[ τ'!▸ ]^ u)
      with ⟨ τ' , ⟨ _ , ⟨ ě' , eq ⟩ ⟩ ⟩ ← ⊢⇐s-⊢⇒s ě rewrite eq
         = ⟨ (τ △) -→ τ' , ⟨ _ , ⟨ ⊢λ x ∶ τ ∙ ě' ^ u , refl ⟩ ⟩ ⟩
    ⊢⇐-⊢⇒ (⊢λ x ∶⸨ τ ⸩∙ ě [ τ₃▸ ∙ τ~̸τ₁ ]^ u)
      with ⟨ τ' , ⟨ _ , ⟨ ě' , eq ⟩ ⟩ ⟩ ← ⊢⇐s-⊢⇒s ě rewrite eq
         = ⟨ (τ △) -→ τ' , ⟨ _ , ⟨ ⊢λ x ∶ τ ∙ ě' ^ u , refl ⟩ ⟩ ⟩
    ⊢⇐-⊢⇒ (⊢⸨_⸩[_∙_] {τ' = τ'} ě τ~̸τ' su) = ⟨ τ' , ⟨ _ , ⟨ ě , refl ⟩ ⟩ ⟩
    ⊢⇐-⊢⇒ (⊢∙_[_∙_]  {τ' = τ'} ě τ~τ' su) = ⟨ τ' , ⟨ _ , ⟨ ě , refl ⟩ ⟩ ⟩

    ⊢⇐s-⊢⇒s : ∀ {Γ τ 𝕞} → (ě : Γ ⊢⇐s τ ∣ 𝕞) → ∃[ τ' ] ∃[ 𝕞' ] Σ[ ě' ∈ Γ ⊢⇒s τ' ∣ 𝕞' ] ě ⇐□s ≡ ě' ⇒□s
    ⊢⇐s-⊢⇒s (⊢∙_[_]  {τ' = τ'} ě τ~τ') = ⟨ τ' , ⟨ _ , ⟨ ě , refl ⟩ ⟩ ⟩
    ⊢⇐s-⊢⇒s (⊢⸨_⸩[_] {τ' = τ'} ě τ~τ') = ⟨ τ' , ⟨ _ , ⟨ ě , refl ⟩ ⟩ ⟩

  private
    ⊢⇒-⊢⇐-subsume : ∀ {Γ τ 𝕞 τ'} → (ě : Γ ⊢⇒ τ ∣ 𝕞) → (su : MSubsumable ě) → Σ[ ě' ∈ Γ ⊢⇐ τ' ∣ 𝕞 ] ě ⇒□ ≡ ě' ⇐□
    ⊢⇒-⊢⇐-subsume {τ = τ} {τ' = τ'} ě su
      with τ' ~? τ 
    ...  | yes τ'~τ = ⟨ ⊢∙ ě [ τ'~τ ∙ su ] , refl ⟩
    ...  | no  τ'~̸τ = ⟨ ⊢⸨ ě ⸩[ τ'~̸τ ∙ su ] , refl ⟩

    ⊢⇒s-⊢⇐s-subsume : ∀ {Γ τ 𝕞 τ'} → (ě : Γ ⊢⇒s τ ∣ 𝕞) → Σ[ ě' ∈ Γ ⊢⇐s τ' ∣ 𝕞 ] ě ⇒□s ≡ ě' ⇐□s
    ⊢⇒s-⊢⇐s-subsume {τ = τ} {τ' = τ'} ě
      with τ' ~? τ 
    ...  | yes τ'~τ = ⟨ ⊢∙ ě [ τ'~τ ] , refl ⟩
    ...  | no  τ'~̸τ = ⟨ ⊢⸨ ě ⸩[ τ'~̸τ ] , refl ⟩

  mutual
    ⊢⇒-⊢⇐ : ∀ {Γ τ 𝕞 τ'} → (ě : Γ ⊢⇒ τ ∣ 𝕞) → Σ[ ě' ∈ Γ ⊢⇐ τ' ∣ 𝕞 ] ě ⇒□ ≡ ě' ⇐□
    ⊢⇒-⊢⇐ ě@(⊢ ∋x ^ u)               = ⊢⇒-⊢⇐-subsume ě MSuVar
    ⊢⇒-⊢⇐ {τ' = τ'} (⊢λ x ∶ τ ∙ ě ^ u)
      with τ' ▸-→?
    ...  | no  τ'!▸ with ⟨ ě' , eq ⟩ ← ⊢⇒s-⊢⇐s ě rewrite eq
         = ⟨ ⊢⸨λ x ∶ τ ∙ ě' ⸩[ τ'!▸ ]^ u , refl ⟩
    ...  | yes ⟨ τ₁ , ⟨ τ₂ , τ'▸ ⟩ ⟩ with (τ △) ~? τ₁
    ...    | yes τ~τ₁ with ⟨ ě' , eq ⟩ ← ⊢⇒s-⊢⇐s ě rewrite eq
           = ⟨ ⊢λ x ∶ τ ∙ ě' [ τ'▸ ∙ τ~τ₁ ]^ u , refl ⟩
    ...    | no  τ~̸τ₁ with ⟨ ě' , eq ⟩ ← ⊢⇒s-⊢⇐s ě rewrite eq
           = ⟨ ⊢λ x ∶⸨ τ ⸩∙ ě' [ τ'▸ ∙ τ~̸τ₁ ]^ u , refl ⟩
    ⊢⇒-⊢⇐ ě@(⊢ ě₁ ∙ ě₂ [ τ▸ ]^ u)    = ⊢⇒-⊢⇐-subsume ě MSuAp1
    ⊢⇒-⊢⇐ ě@(⊢⸨ ě₁ ⸩∙ ě₂ [ τ!▸ ]^ u) = ⊢⇒-⊢⇐-subsume ě MSuAp2
    ⊢⇒-⊢⇐ ě@(⊢ℕ n ^ u)               = ⊢⇒-⊢⇐-subsume ě MSuNum
    ⊢⇒-⊢⇐ ě@(⊢ ě₁ + ě₂ ^ u)          = ⊢⇒-⊢⇐-subsume ě MSuPlus
    ⊢⇒-⊢⇐ ě@(⊢⟦ ∌y ⟧^ u)             = ⊢⇒-⊢⇐-subsume ě MSuFree
    ⊢⇒-⊢⇐ ě@(⊢⋎^ u)                  = ⊢⇒-⊢⇐-subsume ě MSuMultiParent
    ⊢⇒-⊢⇐ ě@(⊢↻^ u)                  = ⊢⇒-⊢⇐-subsume ě MSuUnicycle

    ⊢⇒s-⊢⇐s : ∀ {Γ τ 𝕞 τ'} → (ě : Γ ⊢⇒s τ ∣ 𝕞) → Σ[ ě' ∈ Γ ⊢⇐s τ' ∣ 𝕞 ] ě ⇒□s ≡ ě' ⇐□s
    ⊢⇒s-⊢⇐s ě = ⊢⇒s-⊢⇐s-subsume ě
